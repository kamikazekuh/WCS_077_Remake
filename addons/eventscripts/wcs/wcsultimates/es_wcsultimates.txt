////////////////////////////////////////
// wcs - Warcraft:Source              //
// ---------------------------------- //
//  by kRypT0n!Te                     //
//      http://wcs.despo-clan.de      //
// ---------------------------------- //
////////////////////////////////////////
// wcs - Warcraft:Source : Orangebox  //
// ---------------------------------- //
//  by [Oddity]TeacherCreature        //
//     http://wcs.warcraft-source.net //
// ---------------------------------- //
// Content of this subscript          //
// - all ultimates                    //
////////////////////////////////////////
// commands:
// wcs_mole_activate <userid>
// wcs_regeneration <userid> <amount> <time> <maxHP> <maxHeal> <radius>
// wcs_warden	<userid> <duration> <damage> <radius> <target team> <team target name> <x> <y> <z> <round>
//
////////////////////////////////////////
// You can define here new ultimates, then use the command
// es_xdoblock wcs/wcsultimates/wcs_YOURSCRIPT
// to execute wcs_YOURSCRIPT block.
//

// >>>>>>>>>>>>>>>>> SPECIAL ULTIMATE SKILL CODE >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

block load
{
	es_xset wcs_uid 0
	es_xset wcs_duration 0
	es_xset wcs_dmg 0
	es_xset wcs_radius2 0
	es_xset wcs_team_target 0
	es_xset wcs_wardenround 0
	es_xset wcs_red 0
	es_xset wcs_blue 0
	es_xset wcs_green 0
	es_xset wcs_x3 0
	es_xset wcs_y3 0
	es_xset wcs_z3 0
	es_xexists wcs_exists command wcs_mole_activate
	if (server_var(wcs_exists) = 0) then es_xregcmd wcs_mole_activate wcs/wcsultimates/wcs_mole_activate "A player becomes a mole"
	es_xexists wcs_exists command wcs_regeneration
	if (server_var(wcs_exists) = 0) then es_xregcmd wcs_regeneration wcs/wcsultimates/wcs_regeneration "wcs_regeneration <userid> <amount> <time> <maxHP> <maxHeal> <radius> - Regenerate player for the current round"
	es_xexists wcs_exists command wcs_warden
	if (server_var(wcs_exists) = 0) then es_xregcmd wcs_warden wcs/wcsultimates/wcs_warden "Warden, deals damage on nearby enemies"
}

block wcs_ulti_suicide
{
      es_xset wcs_magnitudef 0
      es_xset wcs_radiusf 0
      es_format wcs_magnitudef "iMagnitude %1" server_var(wcs_magnitude)
      es_format wcs_radiusf "iRadiusoverride %1" server_var(wcs_radius)
      es_xset temphandle 0
      es_give server_var(wcs_userid) env_explosion
      es_fire server_var(wcs_userid) env_explosion addoutput server_var(wcs_magnitudef)
      es_fire server_var(wcs_userid) env_explosion addoutput server_var(wcs_radiusf)
      es_fire server_var(wcs_userid) env_explosion addoutput "spawnflags 8"
      es_getplayerhandle temphandle server_var(wcs_userid)
      es_setindexprop server_var(eventscripts_lastgive) CBaseEntity.m_hOwnerEntity server_var(temphandle)
      es_fire server_var(wcs_userid) env_explosion explode
      es_delayed 0.1 es_xfire server_var(wcs_userid) env_explosion kill
}

block wcs_ulti_tele
{
	es_xset wcs_ok 1
	es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es_xcopy wcs_x wcs_x1
	es_xcopy wcs_y wcs_y1
	es_xcopy wcs_z wcs_z1
	es_math wcs_x - server_var(wcs_x2)
	es_math wcs_y - server_var(wcs_y2)
	es_math wcs_z - server_var(wcs_z2)
	if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
	if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
	if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1
	es wcsgroup get invisp wcs_tmp server_var(wcs_userid)
	if (server_var(wcs_tmp) != 0) do
	{
		es wcs_setfx invisp server_var(wcs_userid) = server_var(wcs_tmp) 1
	}
	ifx true(wcs_ok) do
	{
		es wcsx create wcs_coord server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es wcs pushto server_var(wcs_userid) server_var(wcs_coord) 2
		es usermsg fade server_var(wcs_userid) 0 server_var(wcs_fadetimer) 1 0 0 0 250
	}
	else do
	{
		if (server_var(wcs_range) < server_var(wcs_x)) do
		{
			if (server_var(wcs_x2) >= 0) do
			{
				if (server_var(wcs_x1) >= 0) do
				{
					if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
					if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
				}
				else do
				{
					es_math wcs_x2 - server_var(wcs_range)
				}
			}
			else do
			{
				if (server_var(wcs_x1) < 0) do
				{
					if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
					if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
				}
				else do
				{
					es_math wcs_x2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_x1 wcs_x2
		}
		if (server_var(wcs_range) < server_var(wcs_y)) do
		{
			if (server_var(wcs_y2) >= 0) do
			{
				if (server_var(wcs_y1) >= 0) do
				{
					if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
					if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
				}
				else do
				{
					es_math wcs_y2 - server_var(wcs_range)
				}
			}
			else do
			{
				if (server_var(wcs_y1) < 0) do
				{
					if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
					if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
				}
				else do
				{
					es_math wcs_y2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_y1 wcs_y2
		}
		if (server_var(wcs_range) < server_var(wcs_z)) do
		{
			if (server_var(wcs_z2) >= 0) do
			{
				if (server_var(wcs_z1) >= 0) do
				{
					if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
					if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
				}
				else do
				{
					es_math wcs_z2 - server_var(wcs_range)
				}
			}
			else do
			{
				if (server_var(wcs_z1) < 0) do
				{
					if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
					if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
				}
				else do
				{
					es_math wcs_z2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_z1 wcs_z2
		}
		es wcsx create wcs_coord server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es wcs pushto server_var(wcs_userid) server_var(wcs_coord) 1
		es usermsg fade server_var(wcs_userid) 0 server_var(wcs_fadetimer) 0.2 0 0 0 220
	}
} 

block wcs_ulti_chain
{
	if (server_var(wcs_debug) = "1") then echo wcs_ulti_chain start
	es_xset wcs_team_target 0
	es_xset wcs_targetid 0
	es_getplayerteam wcs_team server_var(wcs_userid)
	// check teams (T=2, CT=3)
	if (server_var(wcs_team) = 2) do
	{
		es_xset wcs_team_target 3
		es_xset wcs_team_targetn "#ct"
	}
	if (server_var(wcs_team) = 3) do
	{
		es_xset wcs_team_target 2
		es_xset wcs_team_targetn "#t"
	}
	if (server_var(wcs_team_target) != "0") do
	{
		es_xset wcs_pcount 0
		es_getlivingplayercount wcs_pcount server_var(wcs_team_target)
		if (server_var(wcs_pcount) > 0) do
		{
			es wcs_fade server_var(wcs_userid) 0 2 1 240 240 240 100
			alias wcs_run_ulti_chain1 "es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid);es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid);es_xmath wcs_z1 + 50;es_xmath wcs_z2 + 50;wcs_run_ulti_chain2"
			alias wcs_run_ulti_chain2 "es_xset wcs_immunity 0;es_keygetvalue wcs_immunity wcsuserdata server_var(wcs_targetid) ulti_immunity;if (server_var(wcs_immunity) = 0) then wcs_run_ulti_chain3;if (server_var(wcs_immunity) != 0) then es_xtell server_var(wcs_userid) #multi server_var(wcs_lng_ulti_immunity_a);if (server_var(wcs_immunity) != 0) then es_xtell server_var(wcs_targetid) #multi server_var(wcs_lng_ulti_immunity_v)"
			alias wcs_run_ulti_chain3 "es_xmath wcs_counter + 1;if (server_var(wcs_graphicfx) = 1) then est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 10 10 200 255 255 255;es wcs damage server_var(wcs_targetid) server_var(wcs_userid) 32"
			es_xset wcs_counter 0
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
			es wcs_nearcoord wcs_targetid server_var(wcs_team_targetn) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_exists wcs_exists userid server_var(wcs_targetid);if (server_var(wcs_exists) = 1) then es_xif (server_var(wcs_counter) < server_var(wcs_maxtargets)) then wcs_run_ulti_chain1"
			if (server_var(wcs_counter) > 0) do
			{
				wcs_getlanguage "wcs_lng" "wcs_lng_chainlightning"
				wcs_getlanguage "wcs_lng2" "wcs_lng_playersdamaged"
				es_tell server_var(wcs_userid) #multi #green server_var(wcs_lng) : server_var(wcs_counter) server_var(wcs_lng2)
			}
			es_xelse do
			{
				es_xset wcs_ultinotexec 1
			}
		}
		es_xelse do
		{
			es_xset wcs_ultinotexec 1
		}
	}
	wcs_getlanguage "wcs_lng" "wcs_lng_chainfailed"
	if (server_var(wcs_ultinotexec) = 1) then es_xtell server_var(wcs_userid) #multi server_var(wcs_lng)
	if (server_var(wcs_debug) = "1") then echo wcs_ulti_chain end
}


block wcs_ulti_roots
{
	if (server_var(wcs_debug) = "1") then echo wcs_ulti_roots start
	es_xset wcs_team_target 0
	es_getplayerteam wcs_team server_var(wcs_userid)
	// check teams (T=2, CT=3)
	if (server_var(wcs_team) = 2) do
	{
		es_xset wcs_team_target 3
		es_xset wcs_team_targetn "#ct"
	}
	if (server_var(wcs_team) = 3) do
	{
		es_xset wcs_team_target 2
		es_xset wcs_team_targetn "#t"
	}
	if (server_var(wcs_team_target) != "0") do
	{
		es_xset wcs_pcount 0
		es_getlivingplayercount wcs_pcount server_var(wcs_team_target)
		if (server_var(wcs_pcount) > 0) do
		{
			es wcs_fade server_var(wcs_userid) 0 1 1 10 55 5 200
			alias wcs_run_ulti_roots1 "es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid);es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid);es_xmath wcs_z1 + 50;es_xmath wcs_z2 + 50;wcs_run_ulti_roots2"
			alias wcs_run_ulti_roots2 "es_xset wcs_immunity 0;es_keygetvalue wcs_immunity wcsuserdata server_var(wcs_targetid) ulti_immunity;if (server_var(wcs_immunity) = 0) then wcs_run_ulti_roots3;if (server_var(wcs_immunity) != 0) then es_xtell server_var(wcs_userid) #multi server_var(wcs_lng_ulti_immunity_a);if (server_var(wcs_immunity) != 0) then es_xtell server_var(wcs_targetid) #multi server_var(wcs_lng_ulti_immunity_v)"
			alias wcs_run_ulti_roots3 "es_xmath wcs_counter + 1;es playerset freeze server_var(wcs_targetid) 1;es_delayed server_var(wcs_freezetime) playerset freeze server_var(wcs_targetid) 0;if (server_var(wcs_graphicfx) = 1) then est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 10 10 0 255 0 255"
			es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_userid)
			es_xset wcs_counter 0
			es wcs_nearcoord wcs_targetid server_var(wcs_team_targetn) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius) "es_exists wcs_exists userid server_var(wcs_targetid);if (server_var(wcs_exists) = 1) then wcs_run_ulti_roots1"
			if (server_var(wcs_counter) > 0) do
			{
				wcs_getlanguage "wcs_lng" "wcs_lng_entangleroots"
				es_tell server_var(wcs_userid) #multi #green server_var(wcs_lng) : server_var(wcs_counter)
			}
			es_xelse do
			{
				es_xset wcs_ultinotexec 1
			}
		}
		es_xelse do
		{
			es_xset wcs_ultinotexec 1
		}
	}
	wcs_getlanguage "wcs_lng" "wcs_lng_entanglefailed"
	if (server_var(wcs_ultinotexec) = 1) then es_xtell server_var(wcs_userid) #multi server_var(wcs_lng)
	if (server_var(wcs_debug) = "1") then echo wcs_ulti_roots end
}

block wcs_regeneration
{
	es_xset wcs_uid 0
	es_xset wcs_amount 0
	es_xset wcs_time 0
	es_xset wcs_maxhp 0
	es_xset wcs_maxheal 0
	es_xset wcs_radius 0
	es_xgetargv wcs_uid 1
	es_xgetargv wcs_amount 2
	es_xgetargv wcs_time 3
	es_xgetargv wcs_maxhp 4
	es_xgetargv wcs_maxheal 5
	es_xgetargv wcs_radius 6
	es_exists wcs_exists userid server_var(wcs_uid)
	if (server_var(wcs_time) < 1) then es_xset wcs_exists 0
	if (server_var(wcs_exists) = 1) do
	{
		es_xset wcs_regenerate 0
		es wcsgroup get regeneration wcs_regenerate server_var(wcs_uid)
		if (server_var(wcs_regenerate) = "1") do
		{
			es wcsgroup get regen_round wcs_round server_var(wcs_uid)
			if (server_var(wcs_roundcounter) = server_var(wcs_round)) do
			{
				es_getplayerprop wcs_dead server_var(wcs_uid) "CCSPlayer.baseclass.pl.deadflag"
				if (server_var(wcs_maxheal) = "0") then es_xset wcs_dead "1"
				if (server_var(wcs_dead) = "0") do
				{
					es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 server_var(wcs_uid)
					// check for team healing
					if (server_var(wcs_radius) > 0) do
					{
						es_getplayerteam wcs_team server_var(wcs_uid)
						// check teams (T=2, CT=3)
						if (server_var(wcs_team) = 2) then es_xset wcs_team "#t"
						if (server_var(wcs_team) = 3) then es_xset wcs_team "#ct"
						es_xmath wcs_z1 + 50
						wcs_getlanguage "wcs_lng" "wcs_lng_healed"
						alias wcs_run_heal "es playerset health server_var(wcs_targetid) server_var(wcs_health);es_tell server_var(wcs_targetid) #multi server_var(wcs_lng);es wcs_fade server_var(wcs_targetid) 0 0.4 0 0 200 25 50;es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid);es_xmath wcs_z2 + 50;es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 2 2 0 255 0 255"
						es wcs_near wcs_targetid server_var(wcs_team) server_var(wcs_radius) server_var(wcs_uid) "es playerget health wcs_health server_var(wcs_targetid);es_math wcs_health + server_var(wcs_amount);if (server_var(wcs_maxhp) > 0) then if (server_var(wcs_maxhp) > server_var(wcs_health)) then wcs_run_heal;if (server_var(wcs_maxhp) = 0) then wcs_run_heal;if (server_var(wcs_maxhp) <= server_var(wcs_health)) then es playerset health server_var(wcs_uid) server_var(wcs_maxhp)"
						es_xmath wcs_maxheal - 1
						es_delayed server_var(wcs_time) wcs_regeneration server_var(wcs_uid) server_var(wcs_amount) server_var(wcs_time) server_var(wcs_maxhp) server_var(wcs_maxheal) server_var(wcs_radius)
					}
					else do
					{
						es playerget health wcs_health server_var(wcs_uid)
						es_math wcs_health + server_var(wcs_amount)
						if (server_var(wcs_maxhp) > 0) then if (server_var(wcs_maxhp) > server_var(wcs_health)) then playerset health server_var(wcs_uid) server_var(wcs_health)
						if (server_var(wcs_maxhp) <= server_var(wcs_health)) then es playerset health server_var(wcs_uid) server_var(wcs_maxhp)
						if (server_var(wcs_maxhp) = 0) then playerset health server_var(wcs_uid) server_var(wcs_health)
						es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_uid)
						es_xmath wcs_z2 + 200
						es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1 2 2 0 255 0 255
						wcs_getlanguage "wcs_lng" "wcs_lng_healed"
						if (server_var(wcs_maxhp) > 0) then if (server_var(wcs_maxhp) > server_var(wcs_health)) then es_tell server_var(wcs_uid) #multi server_var(wcs_lng)
						es_xmath wcs_maxheal - 1
						es_delayed server_var(wcs_time) wcs_regeneration server_var(wcs_uid) server_var(wcs_amount) server_var(wcs_time) server_var(wcs_maxhp) server_var(wcs_maxheal) "0"
					}
				}
			}
			else do
			{
				es_keysetvalue wcsuserdata server_var(wcs_uid) regen_round 0
			}
		}
		//    }
	}
}

block wcs_warden
{
	es_xgetargv wcs_uid 1
	es_xgetargv wcs_duration 2
	es_xgetargv wcs_dmg 3
	es_xgetargv wcs_radius2 4
	es_xgetargv wcs_team_target 5
	es_xgetargv wcs_team_targetn 6
	es_xgetargv wcs_x1 7
	es_xgetargv wcs_y1 8
	es_xgetargv wcs_z1 9
	es_xgetargv wcs_wardenround 10
	es_exists wcs_exists userid server_var(wcs_uid)
	es_getlivingplayercount wcs_alive server_var(wcs_team_target)
	if (server_var(wcs_alive) < 1) then es_xset wcs_exists 0
	if (server_var(wcs_wardenround) != server_var(wcs_roundcounter)) then es_xset wcs_duration 0
	if (server_var(wcs_exists) = 1) do
	{
		if (server_var(wcs_duration) > 0) do
		{
			if (server_var(wcs_radius2) != "0") do
			{
				if (server_var(wcs_team_targetn) != "0") do
				{

					es_getplayerteam wcs_team server_var(wcs_uid)
					if (server_var(wcs_team) = 2) do
					{
						es_xset wcs_red 255
						es_xset wcs_green 0
						es_xset wcs_blue 0
						es_xset wcs_flare effects/redflare.vmt
					}
					if (server_var(wcs_team) = 3) do
					{
						es_xset wcs_red 0
						es_xset wcs_green 0
						es_xset wcs_blue 255
						es_xset wcs_flare effects/blueflare1.vmt
					} 
					es_set wcs_x3 server_var(wcs_x1)
					es_set wcs_y3 server_var(wcs_y1)
					es_set wcs_z3 server_var(wcs_z1)
					es_set wcs_radius4 server_var(wcs_radius2)
					es_math wcs_radius4 * 2
					es_set wcs_radius3 server_var(wcs_radius4)
					es_math wcs_radius3 + 5
					es_math wcs_z3 + 150
					es est_effect 11 #a 0 server_var(wcs_flare) server_var(wcs_x3) server_var(wcs_y3) server_var(wcs_z3) 1.1 2 255
					es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x3) server_var(wcs_y3) server_var(wcs_z3) 1.1 15 1 server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) 255
					es est_effect 10 #a 0 sprites/lgtning.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius4) server_var(wcs_radius3) 1 20 100 1 server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) 100 10
					wcs_getlanguage "wcs_lng" "wcs_lng_wardenhit"
					alias wcs_run_warden "es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_targetid);es_math wcs_z2 + 45;es est_effect 3 #a 0 sprites/lgtning.vmt server_var(wcs_x3) server_var(wcs_y3) server_var(wcs_z3) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 1.1 7 1 server_var(wcs_red) server_var(wcs_green) server_var(wcs_blue) 255;wcs_run_warden2"
					alias wcs_run_warden2 "es wcs damage server_var(wcs_targetid) server_var(wcs_uid) server_var(wcs_dmg);es_tell server_var(wcs_targetid) #multi server_var(wcs_lng)"
					es wcs_nearcoord wcs_targetid server_var(wcs_team_targetn) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_radius2) "es_getplayerprop wcs_dead server_var(wcs_targetid) CCSPlayer.baseclass.pl.deadflag;if (server_var(wcs_dead) = 0) then wcs_run_warden"
					es_xmath wcs_duration - 1
					es_delayed 1 wcs_warden server_var(wcs_uid) server_var(wcs_duration) server_var(wcs_dmg) server_var(wcs_radius2) server_var(wcs_team_target) server_var(wcs_team_targetn) server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_wardenround)
				}
			}
		}
	}
}

block wcs_mole_activate
{
	es_xset wcs_uid 0
	es_xgetargv wcs_uid 1
	es_exists wcs_exists userid server_var(wcs_uid)
	if (server_var(wcs_exists) = 1) do
	{
		es_xset wcs_team_target 0
		es_getplayerteam wcs_team server_var(wcs_uid)
		// check teams (T=2, CT=3)
		if (server_var(wcs_team) = 2) do
		{
			es_xset wcs_team_target 3
			es_xset wcs_team_targetn "#ct"
		}
		if (server_var(wcs_team) = 3) do
		{
			es_xset wcs_team_target 2
			es_xset wcs_team_targetn "#t"
		}
		if (server_var(wcs_team_target) != "0") do
		{
			es_xset wcs_pcount 0
			es_getlivingplayercount wcs_pcount server_var(wcs_team_target)
			if (server_var(wcs_pcount) > 0) do
			{
				es_xset wcs_player 0
				if(server_var(wcs_game) = server_var(wcs_game_css)) then es_xif (server_var(wcs_team) = 3) then playerset model server_var(wcs_uid) "player/t_phoenix"
				if(server_var(wcs_game) = server_var(wcs_game_css)) then es_xif (server_var(wcs_team) = 2) then playerset model server_var(wcs_uid) "player/ct_urban"
				if(server_var(wcs_game) = server_var(wcs_game_dods)) then es_xif (server_var(wcs_team) = 2) then playerset model server_var(wcs_uid) "player/german_assault"
				if(server_var(wcs_game) = server_var(wcs_game_dods)) then es_xif (server_var(wcs_team) = 3) then playerset model server_var(wcs_uid) "player/american_assault"
				es wcs_randplayer wcs_player server_var(wcs_team_targetn)
				es_exists wcs_exists userid server_var(wcs_player)
				if (server_var(wcs_exists) = 1) do
				{
					if (server_var(wcs_team) = 2) do
					{
						if (server_var(wcs_location_c) != "0") do
						{
							es_token wcs_x server_var(wcs_location_c) 1 |
							es_token wcs_y server_var(wcs_location_c) 2 |
							es_token wcs_z server_var(wcs_location_c) 3 |
						}
						else do
						{
							es_getplayerlocation wcs_x wcs_y wcs_z server_var(wcs_player)
						}
					}
					if (server_var(wcs_team) = 3) do
					{
						if (server_var(wcs_location_t) != "0") do
						{
							es_token wcs_x server_var(wcs_location_t) 1 |
							es_token wcs_y server_var(wcs_location_t) 2 |
							es_token wcs_z server_var(wcs_location_t) 3 |
						}
						else do
						{
							es_getplayerlocation wcs_x wcs_y wcs_z server_var(wcs_player)
						}
					}
					// new, delayed teleport
					es_set wcs_tmp server_var(mp_freezetime)
					es_xmath wcs_tmp + 3
					wcs_getlanguage "wcs_lng" "wcs_lng_moledelay"
					es_tell server_var(wcs_uid) #multi server_var(wcs_lng)
					es_delayed server_var(mp_freezetime) es_xtell server_var(wcs_uid) #multi server_var(wcs_lng)
					es_delayed server_var(wcs_tmp) es_setpos server_var(wcs_uid) server_var(wcs_x) server_var(wcs_y) server_var(wcs_z)
					es_keysetvalue "wcsuserdata" server_var(wcs_uid) "ismole" "1"
				}
				wcs_getlanguage "wcs_lng" "wcs_lng_mole"
				es_delayed server_var(mp_freezetime) es_xtell server_var(wcs_uid) #multi server_var(wcs_lng)
			}
			es_xelse do
			{
				wcs_getlanguage "wcs_lng" "wcs_lng_molefailed"
				es_tell server_var(wcs_uid) #multi server_var(wcs_lng)
			}
		}
		es_xelse do
		{
			wcs_getlanguage "wcs_lng" "wcs_lng_molefailed"
			es_tell server_var(wcs_uid) #multi server_var(wcs_lng)
		}
	}
	if (server_var(wcs_debug) = "1") then echo wcs_mole_activate end
}